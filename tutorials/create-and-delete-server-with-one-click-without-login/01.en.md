
## Introduction

This tutorial will help you to create or delete servers by API key with one click on Windows OS without the need to log into the Hetzner Online Cloud.

**Prerequisites**

You may need to get some basic information about _batch scripting_, but you can continue safely the tutorial without reading any resources.


## Step 0 - Confirm your system has cURL.exe

Before starting this tutorial you have to check if your system runs _cURL_. 

The formal way to check it is to run the command `curl --version` in a terminal session. It should return something like:

```bash
curl 7.83.1 (Windows) libcurl/7.83.1 Schannel
Release-Date: 2022-05-13
Protocols: dict file ftp ftps http https imap imaps pop3 pop3s smtp smtps telnet tftp
Features: AsynchDNS HSTS IPv6 Kerberos Largefile NTLM SPNEGO SSL SSPI UnixSockets
```

If you don't have it, you should see:

```bash
'curl' is not recognized as an internal or external command,
operable program or batch file.
```

If you don't have installed cURL in your system, you can download it here: https://curl.se/download.html

## Step 1 - Right click on an empty space on a folder while pressing the shift key and select "Open PowerShell..." or "Open Terminal here" option.

Create a new .bat file and open it with notepad:

```
start cmd
echo.>create-server.bat
notepad create-server.bat
```

We are starting by disabling verbose mode and text color:

```bash
@echo off
title Creating...
SETLOCAL ENABLEDELAYEDEXPANSION
color D
```

## Step 2 - Set your variables

Set your API key and SSH key you took from your API Tokens menu under Cloud Console > Security. Give both write and read permission to the API key.

```bash
set APITOKEN=paste your api token
set SSHkey=paste your ssh key
set ServerName=myserver
set start_after_create=true
set server_type=cx21
set datacenter=nbg1-dc3
```

## Step 3 - Check for your snapshots and images

```bash
"%SystemDrive%\Windows\System32\curl.exe" -H "Authorization: Bearer %APITOKEN%" "https://api.hetzner.cloud/v1/images" | findstr /r /c:"description"
"%SystemDrive%\Windows\System32\curl.exe" -H "Authorization: Bearer %APITOKEN%" "https://api.hetzner.cloud/v1/images" | findstr /r /c:"""id"""
echo.
set snapshotIDstatic=111
set /P snapshotIDstatic=Select snapshot ID: 
cls
```

The first command prints the names of your snapshots and images and the second one is the matching id. Decide which one you want to create and write it to "snapshotIDstatic" variable.

## Step 4 - Check for internet connectivity

Check if internet access is available, otherwise, the script may output false info. If no internet access is detected, exit the script immediately.

```
set internet=0
ping 8.8.8.8 -n 1 -w 3000|findstr /V /i "pinging statistics approx minimum packets"
if errorlevel 1 (set internet=0) else (set internet=1)
if %internet%==0 (echo No Internet Connection!) else (echo Internet connection established.&&goto resume)
echo.
pause
exit

:resume
cls
echo.
echo.
echo.
```

## Step 5 - Check if the snapshot or image is available on your account

If snapshot ID is not exist on your account, inform user and exit.

```
set abc1=0
set abc2=0
set abctemp=0
set snapshotID=0


set staticFound=0
if %snapshotIDstatic% LSS 102 (set snapshotIDstatic=11111111)
for /f "delims=" %%a in ('%SystemDrive%\Windows\System32\curl.exe -H "Authorization: Bearer %APITOKEN%" "https://api.hetzner.cloud/v1/images" ^| findstr /r /c:"""id"""') do (
set abc2=!abc1!
set abctemp=%%a
if "!abctemp:~12,1!" NEQ ":" (set abc1=%%a)
if !abc1:~12^,8!==!snapshotIDstatic! (set staticFound=1)
)

  set snapshotID=!abc2:~12,8!
  if %staticFound%==1 (set snapshotID=%snapshotIDstatic%)

if %snapshotID%==0 (echo Snapshot id couldnt get...&&pause&&exit)

echo.


cls


"%SystemDrive%\Windows\System32\curl.exe" -H "Authorization: Bearer %APITOKEN%" -H "Content-Type: application/json" -d "{ \"name\": \"%ServerName%\", \"datacenter\":\"%datacenter%\", \"server_type\": \"%server_type%\", \"start_after_create\":%start_after_create%, \"image\": \"%snapshotID%\", \"ssh_keys\":[\"%SSHkey%\"]}" -X POST "https://api.hetzner.cloud/v1/servers"|findstr ip
echo.
echo.
echo.
```


## Step 6 - Wait for user activity

Inform user if the the creation of the server is successful or not, and wait for input from the user.

```
echo.
echo Snapshot id %snapshotID% created at: %time:~0,5%
echo.
echo SSH username: root
echo.
echo SSH Password: example
echo.
set /p DUMMY=Press enter key to exit...
```

## Conclusion

In this tutorial, you learnt how to create a server from a snapshot or image by clicking just a desktop shortcut. You may want to dive deeper into `batch scripting` to develop a custom script.

Hope it helps.
